/*

This is the pathmap editor used for customizing pathmaps.

You can quickly copy the compiled code by pressing ctrl+shift+p and running the 'Overwatch Script To Workshop: Copy pathmap editor code' command
in vscode with the Overwatch Script To Workshop extension.

*/

import "!Container.del";

rule: "Commands" -1
{
    DisableInspectorRecording();
    CreateHudText(AllPlayers(), Text:"https://github.com/ItsDeltin/Overwatch-Script-To-Workshop/wiki/Pathfinding", TextColor:Color.Blue, Location:Location.Right, SortOrder:0);

    // Left huds
    CreateHudText(AllPlayers(), Text: "OSTW pathmap editor", Location:Location.Left, SortOrder: 0);
    CreateHudText(AllPlayers(), Text: "Remember to periodically compile your pathmap so you don't lose progress.", TextColor: Color.Orange, Location:Location.Left, SortOrder: 1);
    CreateHudText(AllPlayers(), Text: "――――――――――", Location: Location.Left, SortOrder: 2);
    CreateHudText(AllPlayers(), Text: <"Mode - Press <0> to change", InputBindingString(Button.Interact)>, Location: Location.Left, SortOrder: 3);
    CreateHudText(
        AllPlayers(),
        Text: <"<0>Free Walk\n<1>Place Nodes+Segments\n<2>Add Attributes",
            menuArrow(CurrentMode, 0),
            menuArrow(CurrentMode, 1),
            menuArrow(CurrentMode, 2)
        >,
        Location: Location.Left,
        SortOrder: 4
    );
    CreateHudText(AllPlayers(), Text: "――――――――――", Location: Location.Left, SortOrder: 5);
    CreateHudText(AllPlayers(), Text: <"Change Mode → <0>", InputBindingString(Button.Interact)>, Location: Location.Left, SortOrder: 6);
    CreateHudText(CurrentMode == Mode.Place ? AllPlayers() : null, Text: <"Create Node → <0>\nDelete Node → <1>+<0>", inpStr(Button.PrimaryFire), inpStr(Button.Crouch)>, TextColor: Color.SkyBlue, Location: Location.Left, SortOrder: 7);
    CreateHudText(CurrentMode == Mode.Attribute ? AllPlayers() : null, Text: <"Toggle Current Attribute → <0>", inpStr(Button.PrimaryFire)>, TextColor: Color.SkyBlue, Location: Location.Left, SortOrder: 7);
    CreateHudText(ArrayContains([Mode.Place, Mode.Attribute], CurrentMode) ? AllPlayers() : null, Text: <"Select node → <0>\nDeselect All Nodes → <1>+<0>", inpStr(Button.SecondaryFire), inpStr(Button.Crouch)>, TextColor: Color.LimeGreen, Location: Location.Left, SortOrder: 8);
    CreateHudText(CurrentMode == Mode.Place ? AllPlayers() : null, Text: <"Connect selected nodes → <0>\nDisconnect all selected nodes → <1>+<0>", inpStr(Button.Ability2), inpStr(Button.Crouch)>, TextColor: Color.Green, Location: Location.Left, SortOrder: 9);
    CreateHudText(CurrentMode == Mode.Place ? AllPlayers() : null, Text: <"Cycle Connect Type → <0>", inpStr(Button.Reload)>, TextColor: Color.Orange, Location: Location.Left, SortOrder: 10);
    CreateHudText(CurrentMode == Mode.Attribute ? AllPlayers() : null, Text: <"Increase Attribute Value → <0>\nDecrease Attribute Value → <1>+<0>", inpStr(Button.Reload), inpStr(Button.Crouch)>, TextColor: Color.Orange, Location: Location.Left, SortOrder: 10);
    CreateHudText(AllPlayers(), Text: <"Compile Pathmap → <0>", inpStr(Button.Ultimate)>, TextColor: Color.Turquoise, Location: Location.Left, SortOrder: 11);

    // Top huds
    CreateHudText(
        CurrentMode == Mode.Attribute ? AllPlayers() : null,
        Header   : "Attribute:",
        Text     : FirstAttribute,
        Location : Location.Top,
        SortOrder: -4
    );

    CreateHudText(
        CurrentMode == Mode.Place ? AllPlayers() : null,
        Header: "Connect mode:",
        Text: ["Connect All", "Connect As Path", "Connect As Star"][PlaceMode],
        Location: Location.Top,
        SortOrder: -4
    );

    CreateHudText(
        AllPlayers(),
        Text: " \n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n",
        Location: Location.Top,
        SortOrder: -0.1
    );
}

define inpStr(Button button): InputBindingString(button);

define menuArrow(define value, define expecting): value == expecting ? "▶" : "▷";

globalvar Mode CurrentMode;
enum Mode
{
    Navigate,
    Place,
    Attribute
}

rule: "Enable Conflicting Controls: Free-Walk"
Event.OngoingPlayer
if (CurrentMode == Mode.Navigate)
{
    AllowButton(EventPlayer(), Button.PrimaryFire);
    AllowButton(EventPlayer(), Button.SecondaryFire);
    AllowButton(EventPlayer(), Button.Reload);
    AllowButton(EventPlayer(), Button.Ability2);
    AllowButton(EventPlayer(), Button.Ability1);
}

rule: "Disable Conflicting Controls: Free-Walk"
Event.OngoingPlayer
if (CurrentMode != Mode.Navigate)
{
    DisallowButton(EventPlayer(), Button.PrimaryFire);
    DisallowButton(EventPlayer(), Button.SecondaryFire);
    DisallowButton(EventPlayer(), Button.Reload);
    DisallowButton(EventPlayer(), Button.Ability2);
    DisallowButton(EventPlayer(), Button.Ability1);
}

globalvar define scoper; // The dummy used for reevaluation containers.
globalvar define compiling = false; // Determines if the pathmap is currently compiling.
globalvar Vector nodesOut 0; // The nodes for the CSV output.
globalvar Vector segmentsOut 1; // The segments for the CSV output.
globalvar Vector attributesOut 2; // The attributes for the CSV output.
globalvar Vector preloadNodes 3; // Preloading nodes.
globalvar Vector preloadSegments 4; // Preloading segments.
globalvar Vector preloadAttributes 5; // Preloading segments.
globalvar define fileName = null; // The name of the pathmap file.

globalvar define PlaceMode = 0;
globalvar define FirstAttribute = 0; // The selected attribute.

// Used for updating visible effects.
globalvar define updateVisibleLook;
globalvar define updateVisiblePosition;

globalvar Node[] Nodes = [];
globalvar Node[] Selected = [];
globalvar LinkedAttribute[] Attributes = [];
globalvar Segment[] Segments = [];

Button b_placeNode: Button.PrimaryFire;
Button b_selectNode: Button.SecondaryFire;
Button b_connectNodes: Button.Ability2;
Button b_placeModeAndAttribute: Button.Reload;
Button b_compile: Button.Ultimate;

rule: "Create Scoper"
{
    if (CurrentGameMode() == GameMode.Deathmatch)
        CreateDummyBot(Hero.Roadhog, Team.All, 1, Vector(0, 1000, 0));
    else
        CreateDummyBot(Hero.Roadhog, Team.Team1, 1, Vector(0, 1000, 0));
    scoper = LastCreatedEntity();
    SetGravity(scoper, 0);
    SetStatus(scoper, null, Status.Unkillable, 9999);
    SetMaxHealth(scoper, 1000);
    StartDamageModification(scoper, FilteredArray(AllPlayers(), ArrayElement() != scoper), 0, DamageModificationRev.ReceiversDamagersAndDamagePercent);
    SetInvisible(scoper, InvisibleTo.All);
}

void HelpText(ref define text, ref define order, Color color)
{
    CreateHudText(AllPlayers(), Text:text, TextColor:color, Location:Location.Right, SortOrder:order);
}

rule: "Enable conflicting controls"
Event.OngoingPlayer
if (CurrentMode != Mode.Place)
{
    SetSecondaryFireEnabled(EventPlayer(), true);
}
rule: "Set mode"
if (IsButtonHeld(HostPlayer(), Button.Interact))
{
    CurrentMode++;
    if (CurrentMode > 2) CurrentMode = 0;
}

Node GetClosestNode(): Nodes.SortedArray((Node node) => EyePosition().DistanceTo(node.ShownLocation))[0];
define IsNodeSelectable(Node node): DistanceBetween(node.Pos, EyePosition(EventPlayer())) < 5;

rule: "Create node"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_placeNode))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    ModifyVariable(Nodes, Operation.AppendToArray, new Node(PositionOf(EventPlayer())));
    Nodes.Last.Show();
}

rule: "Delete node"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_placeNode))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    Node selected = GetClosestNode();
    if (!IsNodeSelectable(selected)) return;

    // Delete segments
    for (define i = Segments.Length - 1; i >= 0; i--)
        if (Segments[i].Nodes.Contains(selected))
        {
            Segments[i].Hide();
            delete(Segments[i]);
            Segments = RemoveFromArrayAtIndex(Segments, i);
        }

    // Delete attributes
    for (define i = Attributes.Length - 1; i >= 0; i--)
    {
        Node update = null;
        if (Attributes[i].Node1 == selected)
        {
            update = Attributes[i].Node2;
            delete(Attributes[i]);
            ModifyVariable(Attributes, Operation.RemoveFromArrayByIndex, i);
        }
        if (Attributes[i].Node2 == selected)
        {
            update = Attributes[i].Node1;
            delete(Attributes[i]);
            ModifyVariable(Attributes, Operation.RemoveFromArrayByIndex, i);
        }
        if (update != null)
            update.UpdateAttributeTexts();
    }

    PlayEffect(AllPlayers(), PlayEffect.BadExplosion, Color.Blue, selected.ShownLocation, 0.25);
    selected.Hide();
    Selected = RemoveFromArray(Selected, selected);
    Nodes = RemoveFromArray(Nodes, selected); 
    delete(selected);
}

rule: "Select"
Event.OngoingPlayer
if (CurrentMode == Mode.Place || CurrentMode == Mode.Attribute)
if (IsButtonHeld(EventPlayer(), b_selectNode))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    Node selected = GetClosestNode();
    if (!IsNodeSelectable(selected)) return;

    if (!selected.Selected)
    {
        selected.Select();
        Selected = Append(Selected, selected);
    }
    else
    {
        selected.Deselect();
        Selected = RemoveFromArray(Selected, selected);
    }
}

rule: "Deselect all"
Event.OngoingPlayer
if (CurrentMode == Mode.Place || CurrentMode == Mode.Attribute)
if (IsButtonHeld(EventPlayer(), b_selectNode))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    foreach (Node selected in Selected)
        selected.Deselect();
    Selected = [];
}

class Node
{
    public define Pos;
    public define ShownLocation;
    private define EffectID;
    public define Selected = false;
    public define visible = false;
    public define SelectIDText;
    public define ID: root.Nodes.IndexOf(this);

    public VisibleAttributeInfo[] VisibleAttribute = [];

    public constructor(define pos)
    {
        Pos = pos;
        ShownLocation = Pos + Vector(0, 1.25, 0);
    }

    public void Show()
    {
        if (!visible)
        {
            Update();
            visible = true;
        }
    }

    public void Hide()
    {
        if (visible)
        {
            DestroyInWorldText(SelectIDText);
            DestroyEffect(EffectID);
            visible = false;
            DestroyAttributes();
        }
    }
    
    public void Select()
    {
        Selected = true;
        Update();
    }

    public void Deselect()
    {
        DestroyInWorldText(SelectIDText);
        Selected = false;
        Update();
    }

    private void Update() "Update"
    {
        DestroyEffect(EffectID);
        if (!Selected)
            Make(Color.Blue);
        else
        {
            Make(Color.Green);
            root.ActivateScoper(root.scoper, 1, this);
        }
        EffectID = LastCreatedEntity();
        UpdateAttributeTexts();
    }

    private void DestroyAttributes() "Destroy Attributes"
    {
        foreach (VisibleAttributeInfo visibleAttribute in VisibleAttribute)
        {
            DestroyInWorldText(visibleAttribute.InworldText);
            delete(visibleAttribute);
        }
        VisibleAttribute = [];
    }

    public void UpdateAttributeTexts() "Update Attribute Texts"
    {
        DestroyAttributes();

        LinkedAttribute[] show = root.Attributes.FilteredArray((LinkedAttribute attribute) => attribute.Node1 == this);
        foreach (LinkedAttribute attribute in show)
        {
            define index = VisibleAttribute.IndexOf((VisibleAttribute.FilteredArray((VisibleAttributeInfo info) => info.Link == attribute.Node2))[0]);
            if (index == -1)
            {
                index = VisibleAttribute.Length;
                ModifyVariable(VisibleAttribute, Operation.AppendToArray, new VisibleAttributeInfo(this, attribute.Node2));
                VisibleAttribute[index].String = attribute.ID;
            }
            else
                VisibleAttribute[index].String = <"<0>\n<1>", VisibleAttribute[index].String, attribute.ID>;
        }

        foreach (VisibleAttributeInfo visibleAttribute in VisibleAttribute)
        {
            root.ActivateScoper(root.scoper, 2, visibleAttribute);
            MinWait();
        }
    }

    private void Make(Color color)
    {
        CreateEffect(AllPlayers(), Effect.Sphere, color, ShownLocation, 0.25, EffectRev.VisibleTo);
    }

    public void UpdateVisible()
    {
        if (DistanceBetween(HostPlayer(), ShownLocation) < 35 && (IsInViewAngle(HostPlayer(), ShownLocation, 50) || DistanceBetween(HostPlayer(), ShownLocation) < 5))
            Show();
        else
            Hide();
    }
}

class VisibleAttributeInfo
{
    public Node Source;
    public Node Link;
    public define String = "";
    public define InworldText;

    public constructor(in Node source, in Node link)
    {
        Source = source;
        Link = link;
    }

    public void Make()
    {
        DestroyHudText(InworldText);
        CreateInWorldText(
            AllPlayers(),
            String,
            LinearInterpolate(
                Source.ShownLocation,
                Link.ShownLocation,
                0.2
            ),
            1.5,
            Clipping.ClipAgainstSurfaces,
            InworldTextRev.VisibleTo,
            Color.Yellow,
            Spectators.VisibleAlways
        );
        InworldText = LastTextID();
    }
}

rule: "Connect All Segments"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_connectNodes))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (PlaceMode == 0)
{
    for (define f = 0; f < Selected.Length; f++)
        for (define l = 0; l < Selected.Length; l++)
            if ((l != f) && !SegmentExists(Selected[f], Selected[l]))
            {
                ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[f], Selected[l]));
                Segments.Last.Show();
            }
}

rule: "Connect Segment Path"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_connectNodes))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (PlaceMode == 1)
{
    for (define i = 1; i < Selected.Length; i++)
        if (!SegmentExists(Selected[i - 1], Selected[i]))
        {
            ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[i - 1], Selected[i]));
            Segments.Last.Show();
        }
}

rule: "Connect Segments From First"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_connectNodes))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (PlaceMode == 2)
{
    for (define i = 1; i < Selected.Length; i++)
        if (!SegmentExists(Selected[0], Selected[i]))
        {
            ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[0], Selected[i]));
            Segments.Last.Show();
        }
}

define SegmentExists(define node1, define node2): Segments.IsTrueForAny((Segment segment) => segment.Nodes.Contains(node1) && segment.Nodes.Contains(node2));

rule: "Delete Segments"
Event.OngoingPlayer
if (CurrentMode == Mode.Place)
if (IsButtonHeld(EventPlayer(), b_connectNodes))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    for (define i = Segments.Length - 1; i >= 0; i--)
        if (Segments[i].Nodes.IsTrueForAll((Node node) => Selected.Contains(node)))
        {
            Segments[i].Hide();
            delete(Segments[i]);
            ModifyVariable(Segments, Operation.RemoveFromArrayByIndex, i);
        }
}

class Segment
{
    public Node Node1;
    public Node Node2;    
    public Node[] Nodes;

    define EffectID;
    public define visible = false;

    public constructor(in Node node1, in Node node2)
    {
        Node1 = node1;
        Node2 = node2;
        Nodes = [Node1, Node2];
    }

    public void Show()
    {
        if (!visible)
        {
            CreateBeamEffect(AllPlayers(), BeamType.GrappleBeam, Node1.ShownLocation, Node2.ShownLocation, Color.Red, EffectRev.VisibleTo);
            EffectID = LastCreatedEntity();
            visible = true;
        }
    }
    public void Hide()
    {
        if (visible)
        {
            DestroyEffect(EffectID);
            visible = false;
        }
    }
}

rule: "Update visible"
if (updateVisibleLook == null || DistanceBetween(updateVisiblePosition, HostPlayer()) > 3 || AngleBetweenVectors(updateVisibleLook, FacingDirectionOf(HostPlayer())) > 10)
{
    updateVisibleLook = FacingDirectionOf(HostPlayer());
    updateVisiblePosition = PositionOf(HostPlayer());

    foreach (Segment segment in Segments)
    {
        segment.Node1.UpdateVisible();
        segment.Node2.UpdateVisible();

        if ((segment.Node1.visible) || segment.Node2.visible)
            segment.Show();
        else
            segment.Hide();
        
        MinWait();
    }
    Wait(0.2);
    LoopIfConditionIsTrue();
}

rule: "Compile"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), b_compile))
{
    compiling = true;
    SmallMessage(AllPlayers(), "Compiling...");

    nodesOut = [];
    segmentsOut = [];
    attributesOut = [];

    foreach (Node node in Nodes)
        ModifyVariable(nodesOut, Operation.AppendToArray, node.Pos);
    
    foreach (Segment segment in Segments)
        ModifyVariable(segmentsOut, Operation.AppendToArray, Vector(
            // X is first node
            segment.Node1.ID,
            // Y is second node
            segment.Node2.ID,
            // Z is unused
            0
        ));
    
    foreach (LinkedAttribute attribute in Attributes)
        ModifyVariable(attributesOut, Operation.AppendToArray, Vector(
            attribute.Node1.ID,
            attribute.Node2.ID,
            attribute.ID,
        ));

    SmallMessage(AllPlayers(), "Compile Finished!");
    EnableInspectorRecording();
    # Pathmap is compiled. Copy the current global action set to the clipboard. In vscode, press ctrl+shift+p to open the command pallete, then run the 'Create pathmap from action-set clipboard' command.
    DisableInspectorRecording();
    compiling = false;
}

rule: "Preload"
if (CountOf(preloadNodes) != 0)
{
    // Load nodes
    foreach (Vector preload in preloadNodes)
        ModifyVariable(Nodes, Operation.AppendToArray, new Node(preload));
    
    // Load segments
    foreach (Vector preload in preloadSegments)
        ModifyVariable(Segments, Operation.AppendToArray, new Segment(
            Nodes[preload.X],
            Nodes[preload.Y]
        ));
    
    // Load attributes
    foreach (Vector preload in preloadAttributes)
        ModifyVariable(Attributes, Operation.AppendToArray, new LinkedAttribute(
            Nodes[preload.X],
            Nodes[preload.Y],
            preload.Z
        ));
    
    if (fileName != null)
        CreateHudText(AllPlayers(), Text: fileName, SortOrder: 1, TextColor: Color.Orange, Location: Location.Right);
}

rule: "Increment Place Mode"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), b_placeModeAndAttribute))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (CurrentMode == Mode.Place)
{
    PlaceMode++;
    if (PlaceMode > 2)
        PlaceMode = 0;
}
rule: "Decrement Place Mode"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), b_placeModeAndAttribute))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (CurrentMode == Mode.Place)
{
    PlaceMode--;
    if (PlaceMode < 0)
        PlaceMode = 2;
}
rule: "Increment Attribute"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), b_placeModeAndAttribute))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (CurrentMode == Mode.Attribute)
{
    pressLoop(() => {
        FirstAttribute++;
    });
}
rule: "Decrement Attribute"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), b_placeModeAndAttribute))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (CurrentMode == Mode.Attribute)
{
    pressLoop(() => {
        FirstAttribute--;
    });
}

void pressLoop(const () => void action)
{
    action.Invoke();
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    while (true)
    {
        action.Invoke();
        Wait(0.1, WaitBehavior.AbortWhenFalse);
        AbortIfConditionIsFalse();
    }
}

rule: "Create Selected ID Text"
Event.OnDamageTaken
if (scoper == EventPlayer())
if (ScopeID == 1)
{
    DestroyInWorldText((<Node>ScopeData).SelectIDText);
    CreateInWorldText(
        AllPlayers(),
        IndexOfArrayValue(Nodes, ScopeData),
        (<Node>ScopeData).ShownLocation,
        3,
        Clipping.DoNotClip,
        InworldTextRev.String,
        Color.Green,
        Spectators.VisibleAlways
    );
    (<Node>ScopeData).SelectIDText = LastTextID();
}

rule: "Create Selected ID Text"
Event.OnDamageTaken
if (scoper == EventPlayer())
if (ScopeID == 2)
{
    (<VisibleAttributeInfo>ScopeData).Make();
}

class LinkedAttribute
{
    public Node Node1;
    public Node Node2;
    public define ID;

    public constructor(in Node node1, in Node node2, in define id)
    {
        Node1 = node1;
        Node2 = node2;
        ID = id;
    }
}

rule: "Add attribute"
Event.OngoingPlayer
if (CurrentMode == Mode.Attribute)
if (IsButtonHeld(EventPlayer(), b_placeNode))
if (Selected.Length >= 2)
{
    Node node1 = Selected[Selected.Length - 2];
    Node node2 = Selected.Last;

    LinkedAttribute matching = (Attributes.FilteredArray((LinkedAttribute attribute) => attribute.Node1 == node1 && attribute.Node2 == node2 && attribute.ID == FirstAttribute))[0];
    if (matching != null)
    {
        ModifyVariable(Attributes, Operation.RemoveFromArrayByValue, matching);
        delete(matching);
    }
    else
        ModifyVariable(Attributes, Operation.AppendToArray, new LinkedAttribute(node1, node2, FirstAttribute));
    
    node1.UpdateAttributeTexts();
}