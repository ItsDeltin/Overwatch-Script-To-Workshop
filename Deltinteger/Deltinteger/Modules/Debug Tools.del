rule: "Debug"
{
    CreateHudText(HostPlayer(), null, "Server Load:", ServerLoad(), Location.Left, 0, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Server Load Average:", ServerLoadAverage(), Location.Left, 1, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Server Load Peak:", ServerLoadPeak(), Location.Left, 2, Color.White, Color.White, Color.Blue);
}

rule: "Debug extended"
{
    CreateHudText(HostPlayer(), null, "Eye Position:", EyePosition(HostPlayer()), Location.Left, 3, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Position:", PositionOf(HostPlayer()), Location.Left, 4, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Throttle:", ThrottleOf(HostPlayer()), Location.Left, 5, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Facing:", <"<0> (<1>, <2>)", FacingDirectionOf(HostPlayer()), HorizontalFacingAngleOf(HostPlayer()), VerticalFacingAngleOf(HostPlayer())>, Location.Left, 6, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Ray Cast Hit Position:",
        RayCastHitPosition(
            EyePosition(HostPlayer()),
            Destination(EyePosition(HostPlayer()), FacingDirectionOf(HostPlayer()), 100),
            AllPlayers(),
            HostPlayer(),
            true
        ), Location.Left, 7, Color.White, Color.White, Color.Blue
    );
    CreateHudText(HostPlayer(), null, "Ray Cast Hit Normal:",
        RayCastHitNormal(
            EyePosition(HostPlayer()),
            Destination(EyePosition(HostPlayer()), FacingDirectionOf(HostPlayer()), 100),
            AllPlayers(),
            HostPlayer(),
            true
        ), Location.Left, 8, Color.White, Color.White, Color.Blue
    );
    CreateHudText(HostPlayer(), null, "Ray Cast Hit Player:",
        RayCastHitPlayer(
            EyePosition(HostPlayer()),
            Destination(EyePosition(HostPlayer()), FacingDirectionOf(HostPlayer()), 100),
            AllPlayers(),
            HostPlayer(),
            true
        ), Location.Left, 9, Color.White, Color.White, Color.Blue
    );

    CreateHudText(HostPlayer(), null, "Camera Pos:", <"<0> (<1>)", cameraPos, Destination(cameraPos, FacingDirectionOf(HostPlayer()), 1)>, Location.Left, 10, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Class Memory:", ClassMemory(), Location.Left, 11, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Class Memory Used:", ClassMemoryUsed(), Location.Left, 12, Color.White, Color.White, Color.Blue);
    CreateHudText(HostPlayer(), null, "Class Memory Created:", ClassMemoryRemaining(), Location.Left, 13, Color.White, Color.White, Color.Blue);
}

define globalvar cam ! = false;
define globalvar cameraPos;

rule: "Toggle Free cam"
if (IsCommunicating(HostPlayer(), Communication.VoiceLineDown))
{
    cam = !cam;
}

rule: "Start Camera"
if (cam)
{
    cameraPos = EyePosition(HostPlayer());
    StartCamera(HostPlayer(), cameraPos, cameraPos + FacingDirectionOf(HostPlayer()), 25);
}

rule: "Stop Camera"
if (!cam)
{
    StopCamera(HostPlayer());
}

rule: "TP to camera"
if (cam)
if (IsCommunicating(HostPlayer(), Communication.VoiceLineLeft))
{
    cam = false;
    define storeFacing = FacingDirectionOf(HostPlayer());
    Teleport(HostPlayer(), cameraPos);
    SetFacing(HostPlayer(), storeFacing, Relative.ToWorld);
}

rule: "Move camera"
if (cam)
if (DistanceBetween(Vector(), ThrottleOf(HostPlayer())) > 0)
{
    define x = XOf(ThrottleOf(HostPlayer()));
    define z = ZOf(ThrottleOf(HostPlayer()));

    define right = CrossProduct(FacingDirectionOf(HostPlayer()), Up());

    if (x < 0)
        cameraPos += right / 2;
    else if (x > 0)
        cameraPos += -right / 2;
    
    if (z > 0)
        cameraPos += FacingDirectionOf(HostPlayer()) / 2;
    else if (z < 0)
        cameraPos -= FacingDirectionOf(HostPlayer()) / 2;

    MinWait();
    LoopIfConditionIsTrue();
}

rule: "Move camera down"
if (cam)
if (IsButtonHeld(HostPlayer(), Button.Crouch))
{
    cameraPos += Down() / 2;
    MinWait();
    LoopIfConditionIsTrue();
}

rule: "Move camera up"
if (cam)
if (IsButtonHeld(HostPlayer(), Button.Jump))
{
    cameraPos += Up() / 2;
    MinWait();
    LoopIfConditionIsTrue();
}